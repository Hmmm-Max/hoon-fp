::
::::  /hook/gate/fp/cat
  ::
/?  314
::
::::
!:  =<
=+  fn==>(fl .(p 24, v -149, w 253, r %n))
::
|=  [* [[a=[?(%fl %ce %ne %na %lg %sm) [@s @u] ?(%e %d %h %u)] ~] ~]]
:-  %noun  (lug:m:fn a)
::
::|=  [* [[a=[@s @u] ~] ~]]
:::-  %noun  (sqt:m:fn a)
::
::|=  [* [[a=[? @s @u] b=[? @s @u] ~] ~]]
::=+  [fu=fn(r %u) fd=fn(r %d) fz=fn(r %z) fa=fn(r %a)]
:::-  %noun  "n: {<(div:fn [%f a] [%f b])>} || u: {<(div:fu [%f a] [%f b])>} || d: {<(div:fd [%f a] [%f b])>} || z: {<(div:fz [%f a] [%f b])>} || a: {<(div:fa [%f a] [%f b])>}"
::
|%
++  fn  ::  float, infinity, or NaN
        ::  s=sign, e=exponent, a=arithmetic form
        ::  (-1)^s * a * 2^e
        ::  c=& : signalling NaN, c=| : quiet NaN
        $%  [%f s=? e=@s a=@u]
            [%i s=?]
            [%n c=?]
        ==
::
++  fl
  |_  [p=@u v=@s w=@u r=?(%n %u %d %z %a)]
  ::  p=precision: number of bits in arithmetic form (NOT HIDDEN BIT!!)
  ::  v=minimum value of e
  ::  w=width: max - min value of e
  ::  r=rounding mode: round to nearest (ties to even), round up, round down,
  ::                   round to zero, round to nearest (ties away from zero)
  ::  binary32: [24 -149 253 r] (-149 = -126 - 24 + 1)
  ::
  ++  m                                                 ::  internal functions, constants
    |%
    ::  do not put 0s into these
    ++  rou  |=  [a=[e=@s a=@u]]  ^-  fn
             (rau a %e)
    ::
    ++  rau  |=  [a=[e=@s a=@u] f=?(%e %d %h %u)]  ^-  fn 
             ?-  r
               %n  (lug %ne a f)  %a  (lug %na a f)
               %u  (lug %ce a f)  %d  (lug %fl a f)
               %z  (lug %fl a f)
             ==
    ::
    ++  add  |=  [a=[e=@s a=@u] b=[e=@s a=@u]]  ^-  fn
             =+  q=(dif:si e.a e.b)
             |-  ?.  (syn:si q)  $(b a, a b, q +(q))    ::  a has larger exponent
             =+  [ma=(met 0 a.a) mb=(met 0 a.b)]
             ::~&  "a: {<a>} || b: {<b>} || ma: {<ma>} || mb: {<mb>}"
             =+  ^=  w  %+  dif:si  e.a  %-  sun:si     ::  expanded exponent of a
               ?:  (^gth prc ma)  (^^sub prc ma)  0
             ::~&  "expanded exponent of a: {<w>}"
             =+  ^=  x  %+  sum:si  e.b  %-  sun:si     ::  highest exponent that b reaches
               ?:  |(=(r %n) =(r %a))  mb  (dec mb)     ::  avoid halfway matches for nearest rd
             ::~&  "highest exponent that b reaches: {<x>}"
             ?:  =((cmp:si w x) --1)                    ::  don't actually need to add
               ?+  r  (lag %fl a)
                 %n  (lag %ne a)  %u  (lag %lg a)  %a  (lag %na a)
               ==
             ::~&  "last step: {<[e.b (^^add (lsh 0 (abs:si q) a.a) a.b)]>}"
             (rou [e.b (^^add (lsh 0 (abs:si q) a.a) a.b)])
    ::
    ++  sub  |=  [a=[e=@s a=@u] b=[e=@s a=@u]]  ^-  fn
             =+  q=(dif:si e.a e.b)
             |-  ?.  (syn:si q)                         ::  a has larger exponent
               (fli $(b a, a b, q +(q), r swr))
             =+  [ma=(met 0 a.a) mb=(met 0 a.b)]
             ::~&  "a: {<a>} || b: {<b>} || ma: {<ma>} || mb: {<mb>}"
             =+  ^=  w  %+  dif:si  e.a  %-  sun:si     ::  expanded exponent of a
               ?:  (^gth prc ma)  (^^sub prc ma)  0
             ::~&  "expanded exponent of a: {<w>}"
             =+  ^=  x  %+  sum:si  e.b  %-  sun:si     ::  highest exponent that b reaches
::             ?:  |(=(r %n) =(r %a))  +(mb)  mb        ::  need one more gap digit than addition?
               ?:  |(=(r %n) =(r %a))  mb  (dec mb)
             ::~&  "highest exponent that b reaches: {<x>}"
             ?:  =((cmp:si w x) --1)                    ::  don't actually need to sub
               ?+  r  (lag %sm a)
                 %n  (lag %ne a)  %u  (lag %ce a)  %a  (lag %na a)
               ==
             =+  j=(lsh 0 (abs:si q) a.a)
             |-  ?.  (^gte j a.b)  (fli $(a.b j, j a.b, r swr))
             =+  i=(^^sub j a.b)
             ::~&  "last step: {<[e.b i]>}"
             ?~  -  [%f & zer]  (rou [e.b -])
    ::
    ++  mul  |=  [a=[e=@s a=@u] b=[e=@s a=@u]]  ^-  fn
             (rou (sum:si e.a e.b) (^^mul a.a a.b))
    ::
    ++  div  |=  [a=[e=@s a=@u] b=[e=@s a=@u]]  ^-  fn
             =+  [ma=(met 0 a.a) mb=(met 0 a.b)]
             =+  v=(dif:si (sun:si ma) (sun:si +((^^add mb prc))))
             =.  a  ?:  (syn:si v)  a
             a(e (sum:si v e.a), a (lsh 0 (abs:si v) a.a))
             =+  [q=(^^div a.a a.b) j=(dif:si e.a e.b)]
             ::~&  "q: {<q>} || j: {<j>} || r: {<(mod a.a a.b)>}"
             ?+  r  (rou [j q])
               %u  ?~  (mod a.a a.b)  (lag %ce [j q])  (lag %lg [j q])
               %n  ?~  (mod a.a a.b)  (lag %ne [j q])  (lag %na [j q])
             ==
    ::
    ++  fma  |=  [a=[e=@s a=@u] b=[e=@s a=@u] c=[e=@s a=@u]]  ^-  fn
             (add [(sum:si e.a e.b) (^^mul a.a a.b)] c)
    ::
    ++  fms  |=  [a=[e=@s a=@u] b=[e=@s a=@u] c=[e=@s a=@u] d=?]  ^-  fn
             ?:  d  (sub [(sum:si e.a e.b) (^^mul a.a a.b)] c)
             (sub c [(sum:si e.a e.b) (^^mul a.a a.b)])
    ::
    ::  sqrt((rsh 1 b a))
    ++  isr  |=  [a=@ b=@]  ^-  [@ ?(%e %d %h %u)]
             =.  a  (lsh 1 1 a)  =.  b  +(b)
             =+  [q=(^^div (dec (xeb a)) 2) r=0]
             =+  ^=  c
               |-  =+  s=(^^add r (bex q))
               =+  (^^mul s s)
               ?:  |(=(q 0) =(q b))
                 ?:  (^^lte - a)  [s -]  [r (^^mul r r)]
               ?:  (^^lte - a)  $(r s, q (dec q))  $(q (dec q))
             =+  z=(rsh 0 b -.c)
             ?:  =(+.c a)  [z %e]
             =+  v=(^^add -.c (lsh 0 (dec b) 1))
             =+  y=(^^mul v v)
             ?:  =(y a)  [z %h]  ?:((^^lth y a) [z %u] [z %d])
    ::
    ++  frd  |=  [a=@s]                                 ::  a/2, rounds to -inf
             =+  b=(old:si a)
             ?:  |(-.b =((end 0 1 +.b) 0))
               (new:si -.b (rsh 0 1 +.b))
             (new:si -.b +((rsh 0 1 +.b)))
    ::
    ++  sqt  |=  [a=[e=@s a=@u]]  ^-  fn
             =+  v=(log a)
             ::      2^v <=      a  < 2^(  v + 1)
             ::  2^(v/2) <= sqrt(a) < 2^(v/2 + 1)
             ::  sqrt(a) will have min(prc, v/2-emn+1) digits
             ::
             ::  if sqrt(a) < 2^(emn - 1) in other words v/2 < emn - 1
             ?:  =((cmp:si (frd v) (dif:si emn --1)) -1)
               [%f & ?:(=(r %u) spd zer)]
             ::
             ~&  ""  
             ~&  "a: {<a>}"
             =.  a  =+  [w=(met 0 a.a) x=(^^mul prc 2)]
               =+  ?:((^^lth w x) (^^sub x w) 0)
               =+  ?:  =((dis - 1) (dis (abs:si e.a) 1))  -
                 (^^add - 1)                            ::  enforce even exponent
               a(e (dif:si e.a (sun:si -)), a (lsh 0 - a.a))
             ::
             =+  x=(^^sub (^^div (met 0 a.a) 2) prc)
             =+  [y=(isr a.a x) z=(frd e.a)]
             ~&  "a: {<a>}"
             ~&  "x: {<x>} || y: {<y>} || z: {<z>}"
             !!
    ::
    ++  equ  |=  [a=[e=@s a=@u] b=[e=@s a=@u]]  ^-  ?
             ?.  =((log a) (log b))  |
             ?:  =((cmp:si e.a e.b) -1)
               =((lsh 0 (abs:si (dif:si e.a e.b)) a.b) a.a)
             =((lsh 0 (abs:si (dif:si e.a e.b)) a.a) a.b)
    ::
    ++  lth  |=  [a=[e=@s a=@u] b=[e=@s a=@u]]  ^-  ?
             ?:  =(e.a e.b)  (^^lth a.a a.b)
             =+  c=(cmp:si (log a) (log b))
             ?:  =(c -1)  &  ?:  =(c --1)  |
             ?:  =((cmp:si e.a e.b) -1)
               (^^lth (rsh 0 (abs:si (dif:si e.a e.b)) a.a) a.b)
             (^^lth (lsh 0 (abs:si (dif:si e.a e.b)) a.a) a.b)
    ::
    ++  lte  |=  [a=[e=@s a=@u] b=[e=@s a=@u]]  ^-  ?
             ?:  =(e.a e.b)  (^^lte a.a a.b)
             =+  c=(cmp:si (log a) (log b))
             ?:  =(c -1)  &  ?:  =(c --1)  |
             ?:  =((cmp:si e.a e.b) -1)
               (^^lte a.a (lsh 0 (abs:si (dif:si e.a e.b)) a.b))
             (^^lte (lsh 0 (abs:si (dif:si e.a e.b)) a.a) a.b)
    ::
    ++  log  |=  [a=[e=@s a=@u]]  ^-  @s
             (sum:si (sun:si (dec (met 0 a.a))) e.a)
    ::
    ++  uni  |=  [a=[e=@s a=@u]]
             ?<  =(a.a 0)
             |-  ?:  =((end 0 1 a.a) 1)  a
             $(a.a (rsh 0 1 a.a), e.a (sum:si e.a --1))
    ::
    ::  assumes that (met 0 a.a) <= prc and emn <= e.a!!
    ++  xpd  |=  [a=[e=@s a=@u]]
             =+  (min (abs:si (dif:si e.a emn)) (^^sub prc (met 0 a.a)))
             a(e (dif:si e.a (sun:si -)), a (lsh 0 - a.a))
    ::
    ::  in order: floor, ceiling, nearest (even, away), larger, smaller
    ++  lag  |=  [t=?(%fl %ce %ne %na %lg %sm) a=[e=@s a=@u]]  ^-  fn
             (lug t a %e)
    ::
    ++  lug  |=  [t=?(%fl %ce %ne %na %lg %sm) a=[e=@s a=@u] f=?(%e %d %h %u)]  ^-  fn
             ?~  a.a
               ?-  t
                 %fl  [%f & zer]  %sm  [%f & zer]       ::  warning: [%sm zer %e] gives zero!!
                 %ce  ?:(=(f %e) [%f & zer] [%f & spd])
                 %lg  [%f & spd]
                 %ne  ?:(=(f %u) [%f & spd] [%f & zer])
                 %na  ?:(|(=(f %e) =(f %d)) [%f & zer] [%f & spd])
               ==
             =+  m=(met 0 a.a)
             ::
             =+  ^=  q
               =+  ^=  f                                ::  reduce precision
                 ?:  (^gth m prc)  (^^sub m prc)  0
               =+  ^=  g  %-  abs:si                    ::  enforce min. exp
                 ?:  =((cmp:si e.a emn) -1)  (dif:si emn e.a)  --0
               (max f g)
             =^  b  a  :-  (end 0 q a.a)
             a(e (sum:si e.a (sun:si q)), a (rsh 0 q a.a))
             ::
             ?~  a.a  ?-  t
                 %fl  [%f & zer]  %sm  [%f & zer]
                 %ce  [%f & spd]  %lg  [%f & spd]
                 %ne  ?:  =(f %e)
                        [%f & ?:((^^lte b (bex (dec q))) zer spd)]
                      [%f & ?:((^^lth b (bex (dec q))) zer spd)]
                 %na  [%f & ?:((^^lth b (bex (dec q))) zer spd)]
               ==
             ::
             =.  a  ?:  =(t %fl)  a  (xpd a)            ::  expand
             ::
             =.  a  %-  uni
               ?-  t
                 %fl  a  %lg  a(a +(a.a))
                 %sm  ?:  &(=(b 0) =(f %e))  a(a (dec a.a))  a
                 %ce  ?:  &(=(b 0) =(f %e))  a  a(a +(a.a))
                 %ne  ?~  b  ?.  =(q 0)  a
                        ?-  f
                          %e  a  %d  a  %u  a(a +(a.a))
                          %h  ?~  (dis a.a 1)  a  a(a +(a.a))
                        ==
                      =+  y=(bex (dec q))
                      ?:  &(=(b y) =(f %e))             ::  halfway rounds to even
                        ?~  (dis a.a 1)  a  a(a +(a.a))
                      ?:  (^^lth b y)  a  a(a +(a.a))
                 %na  ?~  b  ?.  =(q 0)  a
                        ?-  f
                          %e  a  %u  a(a +(a.a))
                          %d  a  %h  a(a +(a.a))
                        ==
                      ?:  (^^lth b (bex (dec q)))  a  a(a +(a.a))
               ==
             ?~  a.a  [%f & zer]
             ::
             =+  x=(dif:si e.a emx)
             =.  a  ?.  (syn:si x)  a                   ::  enforce max. exp
               a(e emx, a (lsh 0 (abs:si x) a.a))
             ::
             ?:  (^gth (met 0 a.a) prc)  [%i &]  [%f & a]
    ::
    ++  swr  ?+(r r %d %u, %u %d)
    ++  prc  p
    ++  emn  v
    ++  emm  (sum:si emn (sun:si (dec p)))
    ++  emx  (sum:si emn (sun:si w))
    ++  spd  [emn 1]                                    ::  smallest "denormal"
    ++  spn  [emm 1]                                    ::  smallest "normal"
    ++  lfn  [emx (fil 0 prc 1)]                        ::  largest
    ++  zer  [--0 0]                                    ::  zero
    --
  ::
  ++  rou  |=  [a=fn]  ^-  fn
           ?.  ?=([%f *] a)  a
           ?~  a.a  [%f s.a --0 0]
           ?:  s.a  (rou:m +>.a)
           =.(r swr:m (fli (rou:m +>.a)))
  ::
  ++  fli  |=  [a=fn]  ^-  fn
           ?-(-.a %f a(s !s.a), %i a(s !s.a), %n a)
  ::
  ++  add  |=  [a=fn b=fn]  ^-  fn
           ?:  |(?=([%n *] a) ?=([%n *] b))  [%n |]
           ?:  |(?=([%i *] a) ?=([%i *] b))
             ?:  &(?=([%i *] a) ?=([%i *] b))
               ?:  =(a b)  a  [%n |]
             ?:  ?=([%i *] a)  a  b
           ?:  |(=(a.a 0) =(a.b 0))
             ?.  &(=(a.a 0) =(a.b 0))  %-  rou  ?~(a.a b a)
             [%f ?:(=(r %d) &(s.a s.b) |(s.a s.b)) --0 0]
           ?:  =(s.a s.b)
             ?:  s.a  (add:m +>.a +>.b)
             =.(r swr:m (fli (add:m +>.a +>.b)))
           ?:  s.a  (sub:m +>.a +>.b)
           (sub:m +>.b +>.a)
  ::
  ++  sub  |=  [a=fn b=fn]  ^-  fn  (add a (fli b))
  ::
  ++  mul  |=  [a=fn b=fn]  ^-  fn
           ?:  |(?=([%n *] a) ?=([%n *] b))  [%n |]
           ?:  ?=([%i *] a)
             ?:  ?=([%i *] b)  [%i =(s.a s.b)]
             ?:  =(a.b 0)  [%n |]  [%i =(s.a s.b)]
           ?:  ?=([%i *] b)
             ?:  =(a.a 0)  [%n |]  [%i =(s.a s.b)]
           ?:  |(=(a.a 0) =(a.b 0))  [%f =(s.a s.b) --0 0]
           ?:  =(s.a s.b)  (mul:m +>.a +>.b)
           =.(r swr:m (fli (mul:m +>.a +>.b)))
  ::
  ++  div  |=  [a=fn b=fn]  ^-  fn
           ?:  |(?=([%n *] a) ?=([%n *] b))  [%n |]
           ?:  ?=([%i *] a)
             ?:  ?=([%i *] b)  [%n |]  [%i =(s.a s.b)]
           ?:  ?=([%i *] b)  [%f =(s.a s.b) --0 0]
           ?:  =(a.a 0)  ?:  =(a.b 0)  [%n |]  [%f =(s.a s.b) --0 0]
           ?:  =(a.b 0)  [%i =(s.a s.b)]
           ?:  =(s.a s.b)  (div:m +>.a +>.b)
           =.(r swr:m (fli (div:m +>.a +>.b)))
  ::
  ++  fma  |=  [a=fn b=fn c=fn]  ^-  fn                 ::  a * b + c
           ?:  |(?=([%n *] a) ?=([%n *] b) ?=([%n *] c))  [%n |]
           =+  ^=  x
             ?:  ?=([%i *] a)
             ?:  ?=([%i *] b)  [%i =(s.a s.b)]
               ?:  =(a.b 0)  [%n |]  [%i =(s.a s.b)]
             ?:  ?=([%i *] b)
               ?:  =(a.a 0)  [%n |]  [%i =(s.a s.b)]
             ?:  |(=(a.a 0) =(a.b 0))  [%f =(s.a s.b) --0 0]
             [%f =(s.a s.b) (sum:si e.a e.b) (^mul a.a a.b)]
           (add x c)
  ::
  ++  sqt  |=  [a=fn]  ^-  fn
           ?:  ?=([%n *] a)  [%n |]
           ?:  ?=([%i *] a)  ?:(s.a a [%n |])
           ?~  a.a  [%f s.a --0 0]
           ?.  s.a  [%n |]
           (sqt:m +>.a)
  ::
  ++  equ  |=  [a=fn b=fn]  ^-  (unit ,?)
           ?:  |(?=([%n *] a) ?=([%n *] b))  ~  :-  ~
           ?:  =(a b)  &
           ?:  |(?=([%i *] a) ?=([%i *] b))  |
           ?:  |(=(a.a 0) =(a.b 0))
             ?:  &(=(a.a 0) =(a.b 0))  &  |
           ?:  |(=(e.a e.b) !=(s.a s.b))  |
           (equ:m +>.a +>.b)
  ::
  ++  lth  |=  [a=fn b=fn]  ^-  (unit ,?)
           ?:  |(?=([%n *] a) ?=([%n *] b))  ~  :-  ~
           ?:  =(a b)  |
           ?:  ?=([%i *] a)  !s.a  ?:  ?=([%i *] b)  s.b
           ?:  |(=(a.a 0) =(a.b 0))
             ?:  &(=(a.a 0) =(a.b 0))  |
             ?:  =(a.a 0)  s.b  !s.a
           ?:  !=(s.a s.b)  s.b
           |-  ?:  =(s.a |)  $(a [%f & e.b a.b], b [%f & e.a a.a])
           (lth:m +>.a +>.b)
  ::
  ++  lte  |=  [a=fn b=fn]  ^-  (unit ,?)
           ?:  |(?=([%n *] a) ?=([%n *] b))  ~  :-  ~
           ?:  =(a b)  &
           ?:  ?=([%i *] a)  !s.a  ?:  ?=([%i *] b)  s.b
           ?:  |(=(a.a 0) =(a.b 0))
             ?:  &(=(a.a 0) =(a.b 0))  &
             ?:  =(a.a 0)  s.b  !s.a
           ?:  !=(s.a s.b)  s.b
           |-  ?:  =(s.a |)  $(a [%f & e.b a.b], b [%f & e.a a.a])
           (lte:m +>.a +>.b)
  ::
  ++  gth  |=  [a=fn b=fn]  ^-  (unit ,?)  (lth b a)
  ::
  ++  gte  |=  [a=fn b=fn]  ^-  (unit ,?)  (lte b a)
  ::
  ::  =((uni a) (uni b)) if and only if (equ a b), with the
  ::  exception of positive/negative 0, which are distinct in
  ::  ++uni but considered equal in comparisons
  ++  uni  |=  [a=fn]  ^-  fn
           ?.  ?=([%f *] a)  a
           ?:  =(a.a 0)  [%f s.a --0 0]
           [%f s.a (uni:m +>.a)]
  --
--
