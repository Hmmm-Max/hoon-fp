::
::::  /hook/gate/fp/cat
  ::
/?  314
::
::::
!:  =<
|=  [* [[a=[? @s @u] b=[? @s @u] ~] ~]]
:-  %noun  (lte:fl [%f a] [%f b])
::|=  [* [[a=[? @s @u] ~] ~]]
:::-  %noun  (uni:fl [%f a])
::
|%
++  fn  ::  float, infinity, or NaN
        ::  s=sign, e=exponent, a=arithmetic form
        ::  (-1)^s * a * 2^e
        ::  c=& : signalling NaN, c=| : quiet NaN
        $%  [%f s=? e=@s a=@u]
            [%i s=?]
            [%n c=?]
        ==
::
++  fl
  |_  [p=@u w=@u r=?(%n %u %d %z) d=?]
  ::  p=precision: number of bits in arithmetic form (NOT HIDDEN BIT!!)
  ::  w=width: number of bits in exponent
  ::  r=rounding mode: round to nearest, round up, round down, round to 0
  ::  d="denormalized": simulate denormalized for ieee754 compliance
  ++  equ  |=  [a=fn b=fn]  ^-  (unit ,?)
           ?:  |(?=([%n *] a) ?=([%n *] b))  ~  :-  ~
           ?:  =(a b)  &
           ?:  |(?=([%i *] a) ?=([%i *] b))  |
           ?:  &(=(a.a 0) =(a.b 0))  &
           ?:  |(=(e.a e.b) !=(s.a s.b))  |
           ?:  =((cmp:si e.a e.b) -1)                   ::  e.a < e.b
             =((lsh 0 (abs:si (dif:si e.a e.b)) a.b) a.a)
           =((lsh 0 (abs:si (dif:si e.a e.b)) a.a) a.b)
  ::
  ++  lth  |=  [a=fn b=fn]  ^-  (unit ,?)
           ?:  |(?=([%n *] a) ?=([%n *] b))  ~  :-  ~
           ?:  =(a b)  |
           ?:  ?=([%i *] a)  !s.a  ?:  ?=([%i *] b)  s.b
           ?:  &(=(a.a 0) =(a.b 0))  |
           ?:  !=(s.a s.b)  s.b
           |-  ?:  =(s.a |)  $(a [%f & e.b a.b], b [%f & e.a a.a])
           ?:  =(e.a e.b)  (^lth a.a a.b)
           ?:  =((cmp:si e.a e.b) -1)
             (^lth (rsh 0 (abs:si (dif:si e.a e.b)) a.a) a.b)
           (^lth (lsh 0 (abs:si (dif:si e.a e.b)) a.a) a.b)
  ::
  ++  lte  |=  [a=fn b=fn]  ^-  (unit ,?)
           ?:  |(?=([%n *] a) ?=([%n *] b))  ~  :-  ~
           ?:  =(a b)  &
           ?:  ?=([%i *] a)  !s.a  ?:  ?=([%i *] b)  s.b
           ?:  &(=(a.a 0) =(a.b 0))  &
           ?:  !=(s.a s.b)  s.b
           |-  ?:  =(s.a |)  $(a [%f & e.b a.b], b [%f & e.a a.a])
           ?:  =(e.a e.b)  (^lte a.a a.b)
           ?:  =((cmp:si e.a e.b) -1)
             (^lte a.a (lsh 0 (abs:si (dif:si e.a e.b)) a.b))
           (^lte (lsh 0 (abs:si (dif:si e.a e.b)) a.a) a.b)
  ::
  ++  gth  |=  [a=fn b=fn]  ^-  (unit ,?)  (lth b a)
  ::
  ++  gte  |=  [a=fn b=fn]  ^-  (unit ,?)  (lte b a)
  ::
  ++  uni  |=  [a=fn]  ^-  fn
           ?.  ?=([%f *] a)  a
           ?:  =(a.a 0)  [%f s.a --0 0]
           |-  ^-  fn
           ?:  =((dis a.a 1) 1)  a
           $(a.a (rsh 0 1 a.a), e.a (sum:si e.a --1))
  ::
  ++  rnd  |=  [a=fn]
           !!
  --
--
